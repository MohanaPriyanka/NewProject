/*************************************************************************************
 * Created By: peteryao on 1/20/19
 * Description:
 * Test:
 *************************************************************************************/
@IsTest
public with sharing class AccountsSelectorTest {
    @TestSetup public static void testSetup() {
        Util.disableAllTriggers();
        Test.loadData(Utility__c.SObjectType, 'TestCSUtility');
        Test.loadData(Utility_NMC_Tariff__c.SObjectType, 'TestCSUtilityNMCTariff');
        Test.loadData(Load_U__c.SObjectType, 'TestCSLoadU');
        Test.loadData(ChargentBase__Gateway__c.SObjectType, 'TestCSGateway');
        Test.loadData(Contact.SObjectType, 'TestCSContact');
        Test.loadData(Account.SObjectType, 'TestCSAccount');
        Test.loadData(Entity__c.SObjectType, 'TestCSEntity');
        Test.loadData(Shared_Solar_System__c.SObjectType, 'TestCSSharedSolarSystem');
        Test.loadData(Utility_Account_Log__c.SObjectType, 'TestCSUtilityAccountLog');
        Test.startTest();
        Test.loadData(Partner__c.SObjectType,'TestRLPartner');
        Test.loadData(Opportunity.SObjectType, 'TestCSOpportunity');
        Test.loadData(Utility_Account_Subscription__c.SObjectType, 'TestCSUtilityAccountSubscription');
        Test.loadData(Schedule_Z__c.SObjectType, 'TestCSScheduleZ');
        Test.loadData(Schedule_Z_Subscription__c.SObjectType, 'TestCSScheduleZSubscription');
        Test.loadData(Bill_Period__c.SObjectType, 'TestCSBillPeriod');
        Test.loadData(Production__c.SObjectType, 'TestCSProduction');
        Test.loadData(Transfer__c.SObjectType, 'TestCSTransfer');
        Test.loadData(Account_Bill__c.SObjectType, 'TestCSAccountBill');
        Test.loadData(System_Bill__c.SObjectType, 'TestCSSystemBill');
        Test.loadData(Utility_Account_Bill__c.SObjectType, 'TestCSUtilityAccountBill');
        Test.loadData(UASB__c.SObjectType, 'TestCSUASB');
        Test.stopTest();
        Util.enableAllTriggers();
    }
    @IsTest public static void testSelectCS67() {
        System.assert(AccountsSelector.selectCS67DaysPastDueWithAnchorOpps() != null, 'Expected a list of accounts');
    }
    @IsTest public static void testSelectCSNotRemoved() {
        System.assert(AccountsSelector.selectCSNotRemoved() != null, 'Expected a list of accounts');
    }
    @IsTest public static void testSelectNotClosed() {
        Set<Id> accountIds = (new Map<Id, Account>([SELECT Id FROM Account])).keySet();
        System.assert(AccountsSelector.selectNotClosed(accountIds) != null, 'Expected a list of accounts');
    }
    @IsTest public static void testSelectWithSendBillsContact() {
        Account autoEmailerAccount = [SELECT Id FROM Account WHERE Name = 'AutoEmailer Account A'];
        System.assertEquals(
            'example@example.com',
            AccountsSelector.selectWithSendBillsContact(new Set<Id>{autoEmailerAccount.Id})[0].Send_Bills_Contact__r.Email,
            'Expected to find an account with send bills contact'
        );
    }
    @IsTest public static void testSelectOne() {
        List<Account> accountIds = (new List<Account>([SELECT Id FROM Account]));
        AccountsSelector accountsSelector = new AccountsSelector();
        System.assert(accountsSelector.selectOne(accountIds.get(0).Id) != null, 'Expected a single account');
    }
    @IsTest public static void testSelectAll() {
        Set<Id> accountIds = (new Map<Id, Account>([SELECT Id FROM Account])).keySet();
        AccountsSelector accountsSelector = new AccountsSelector();
        System.assert(accountsSelector.selectAll(accountIds) != null, 'Expected a list of accounts');
    }
    @IsTest public static void testSelectByIds() {
        Set<Id> accountIds = (new Map<Id, Account>([SELECT Id FROM Account])).keySet();
        AccountsSelector accountsSelector = new AccountsSelector();
        System.assert(accountsSelector.selectAll(accountIds) != null, 'Expected a list of accounts');
    }
    @IsTest public static void testSelectPropertyAccountsByLeadIds() {
        Lead newLead = new Lead(
            FirstName = 'test',
            LastName = 'test',
            Company = 'Test Company'
        );
        insert newLead;
        Id propertyRecordTypeId = Schema.SObjectType.Account.getRecordTypeInfosByName().get('Property').getRecordTypeId();
        Account propertyAccount = new Account(
            RecordTypeId = propertyRecordTypeId,
            Lead__c = newLead.Id,
            Name = 'Test Account'
        );
        insert propertyAccount;
        AccountsSelector accountsSelector = new AccountsSelector();
        System.assertEquals(accountsSelector.selectPropertyAccountsByLeadIds(new Set<Id>{newLead.Id}).size(), 1, 'Expected only one property account to be returned');
    }
    @IsTest public static void testselectAccountsWithBillMessages() {
        Id propertyRecordTypeId = Schema.SObjectType.Account.getRecordTypeInfosByName().get('Property').getRecordTypeId();
        Account propertyAccount = new Account(
            RecordTypeId = propertyRecordTypeId,
            Name = 'Test Account',
            Zuora_Invoice_Message_2_of_2__c = 'XXX',
            Zuora_Invoice_Message_1_of_2__c = 'xxx'
        );
        insert propertyAccount;
        System.assertEquals(1,AccountsSelector.selectAccountsWithBillMessages().size());
    }
    @IsTest
    public static void testSelectForReconciliation() {
        List<String> zuoraAccountIds = new List<String>{'2c92c0f86ab120de016ab427c9032366', '2c92c0f86ab120de016ab427c9032362'};
        AccountsSelector accountSelector = new AccountsSelector();
        List<Map<String, Object>> accounts = accountSelector.selectForReconciliation(zuoraAccountIds);
        System.assertEquals(2, accounts.size());
        zuoraAccountIds = new List<String>{'foo', 'bar'};
        accounts = accountSelector.selectForReconciliation(zuoraAccountIds);
        System.assertEquals(0, accounts.size());
    }
    @IsTest
    public static void testSelectById() {
        List<Account> accounts = [
            SELECT Id
            FROM Account
            LIMIT 2
        ];
        AccountsSelector selector = new AccountsSelector();
        List<Account> queriedAccounts = selector.getAccountsById(new Set<Id>{accounts[0].Id, accounts[1].Id});
        System.assertEquals(2,queriedAccounts.size());
    }
    
    @IsTest public static void testSelectByBlueWaveId() {
        Account accountOne = new Account(
            Name = 'Test Account'
        );
        insert accountOne;
        Account acct = [
            SELECT Id, Account_Number__c
            FROM Account
            WHERE Id = : accountOne.Id
            LIMIT 1
        ];
        AccountsSelector selector = new AccountsSelector();
        Map<String,Account> acctMap = selector.selectByBlueWaveId(new Set<String>{acct.Account_Number__c});
        System.assertEquals(accountOne.Id,acctMap.get(acct.Account_Number__c).Id);
    }

    @IsTest
    public static void testSelectDefaultPartnerAccount() {
        Account partnerAccount = new Account(
            Name = 'Default Partner Account'
        );
        insert partnerAccount;
        TestFactory.setDefaultPartnerAccount(partnerAccount.Id);
        Test.startTest();
        Account defaultPartnerAccount = AccountsSelector.selectDefaultPartnerAccount();
        Test.stopTest();
        System.assertEquals(partnerAccount.Id, defaultPartnerAccount.Id, 'Expected the default partner Account to be returned');
    }

    @IsTest
    public static void testGetAccountsById() {
        System.assertNotEquals(null, new AccountsSelector().getAccountsById(new Set<Id>()),
            'Expected an empty list when selecting by an empty set');
    }

    @IsTest
    public static void testGetPropertiesFromParent() {
        System.assertNotEquals(null, new AccountsSelector().getPropertiesFromParent(new Set<Id>()),
            'Expected an empty list when selecting by an empty set');
    }

    @IsTest
    public static void testSelectPartnerAccount() {
        System.assertEquals(null, AccountsSelector.selectPartnerAccount(Util.getFakeId(Account.SObjectType)),
            'Expected to handle selecting a partner account that doesn\'t exist');
    }

    @IsTest
    public static void testGetRetiredAccounts() {
        Id propertyRecordTypeId = Schema.SObjectType.Account.getRecordTypeInfosByName().get('Property').getRecordTypeId();
        Account propertyAccount = new Account(
            RecordTypeId = propertyRecordTypeId,
            Name = 'Test Account',
            Date_Removed_from_Project__c = System.today()
        );
        insert propertyAccount;
        System.assertNotEquals(null, new AccountsSelector().getRetiredAccounts(new Set<Id>{propertyAccount.Id}),
            'Expected to return Accounts with Date Removed from Project');
    }

}